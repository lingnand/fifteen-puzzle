apply plugin: 'com.android.application'
android {
    compileSdkVersion 22
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "org.cocos.fifteen_puzzle"
        minSdkVersion 9
        targetSdkVersion 9

        // ndk {
        //     moduleName "cocos2dcpp_shared"
        // }
    }

    sourceSets.main {
        java.srcDir "src"
        aidl.srcDir "src"
        res.srcDir "res"
        jniLibs.srcDir "libs"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "assets"
    }

    signingConfigs {

       release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
    }


    applicationVariants.all { variant ->
        task "open${variant.name.capitalize()}" {
            dependsOn "install${variant.name.capitalize()}"
            doLast {
                exec {
                    commandLine "adb shell monkey -p ${variant.applicationId} -c android.intent.category.LAUNCHER 1".split(" ")
                }
            }
        }
    }
}

dependencies {
    compile files('jars/android-async-http-1.4.8.jar')
}